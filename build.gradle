buildscript {
    repositories { jcenter() }
}

plugins {
    id 'java' // or 'groovy' Must be explicitly applied
    id 'com.github.johnrengelman.shadow' version "2.0.2"
}


def localBuildFile = "./build-local.gradle"
def useLocalBuild = new File(localBuildFile).exists()
if (useLocalBuild) {
    apply from : localBuildFile
}

defaultTasks 'shadowJar'

if (!JavaVersion.current().java8Compatible) {
    throw new IllegalStateException('''Nucleus:
                                      |  This needs Java 8,
                                      |  You are using something else,
                                      |  Refresh. Try again.'''.stripMargin())
}

// Define a version registry so that we can avoid hard coding of the version numbers
// Note that this applies only to dependencies and not to the plugins and build
ext {
    vertxVersion = '3.5.0'
    slf4jVersion = '1.7.12'
    junitVersion = '4.12'
    logbackVersion = '1.0.13'
    redisVersion = '2.8.0'
    javaxMailVersion = '1.4.7'
    bouncycastleVersion = '1.46'
    velocityVersion = '1.7'
}

version = "0.1-snapshot"
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url = 'http://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    // Vertx specific dependencies
    compile "io.vertx:vertx-core:${vertxVersion}"
    compile "io.vertx:vertx-web:${vertxVersion}"
    compile "io.vertx:vertx-hazelcast:${vertxVersion}"
    compile "io.vertx:vertx-dropwizard-metrics:${vertxVersion}"
    compile "io.vertx:vertx-web:${vertxVersion}"
    
    // Other dependencies
    compile "ch.qos.logback:logback-classic:${logbackVersion}"
    compile "redis.clients:jedis:${redisVersion}"
    compile "javax.mail:mail:${javaxMailVersion}"
    compile "org.bouncycastle:bcprov-jdk16:${bouncycastleVersion}"
    compile "org.apache.velocity:velocity:${velocityVersion}"


    // Test dependencies
    testCompile "junit:junit:${junitVersion}"

}

shadowJar {
    classifier = 'fat'
    manifest {
        attributes 'Main-Class': 'io.vertx.core.Launcher'
        attributes 'Main-Verticle': 'org.gooru.nucleus.utils.bootstrap.ServerVerticle'
        attributes 'Created-By': 'Gooru.org'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4'
}

